---
title: "Task management with org-roam Vol. 4: Automatic tagging"
tags: emacs, org-roam, org-mode
description: "Automatic tagging upon link insertion"
publish: true
---

In the [[https://d12frosted.io/posts/2020-06-25-task-management-with-roam-vol3.html][previous article]] we covered automatic tagging of notes related to a
specific person, and today we are going to cover automatic tagging of an
org-mode heading upon insertion of link related to a person. To put it simple,
when I mention someone in the task, I would love this task to be automatically
tagged with that persons name. As they say, it's better to see once, than
imagine multiple times, so here is a screencast.

#+BEGIN_EXPORT html
<div class="post-image">
<img src="/images/org-notes-insert.gif" />
</div>
#+END_EXPORT

*Change Log:*

- ~[2021-01-24 Sun]~: Since some of the functionality mentioned in the original
  article was merged to =org-roam=, all code is updated to reflect the current
  state of affairs.
- ~[2021-03-02 Tue]~: Update naming convention to match [[https://github.com/d12frosted/environment/tree/master/emacs][personal configurations]].
- ~[2021-05-10 Mon]~: Update post to reflect changes in [[https://github.com/org-roam/org-roam/pull/1401][org-roam v2]]. Previous
  version of this article is available on [[https://github.com/d12frosted/d12frosted.io/blob/c16870cab6ebbaafdf73c7c3589abbd27c20ac52/posts/2020-07-07-task-management-with-roam-vol4.org][GitHub]].

#+BEGIN_HTML
<!--more-->
#+END_HTML

The solution is based on the relatively recent [[https://github.com/org-roam/org-roam/pull/839][pull request]] to the org-roam.
Instead of using =org-roam-node-insert= directly, we can write a wrapper for it
leveraging the return value, name manipulation from the [[https://d12frosted.io/posts/2020-06-25-task-management-with-roam-vol3.html][previous article]] and
tags lookup from [[https://d12frosted.io/posts/2020-06-10-org-roam-tags.html][Org-roam tags]] article.

#+begin_src emacs-lisp
  (defun vulpea-insert ()
    "Insert a link to the note."
    (interactive)
    (when-let*
        ((node (org-roam-node-insert))
         (title (org-roam-node-title node))
         (tags (org-roam-node-tags node)))
      (when (seq-contains-p tags "people")
        (save-excursion
          (ignore-errors
            (org-back-to-heading)
            (org-set-tags
             (seq-uniq
              (cons
               (vulpea--title-to-tag title)
               (org-get-tags nil t)))))))))
#+end_src

The implementation is straight-forward. We start with calling
=org-roam-node-insert= that asks for the note to insert. Then we parse result
and query the roam tags to understand if the inserted note is related to a
person. And if the answer is yes, we use =org-set-tags= to automatically tag the
heading.

In my case, implementation of =vulpea--title-to-tag= is the following.

#+begin_src emacs-lisp
  (defun vulpea--title-to-tag (title)
    "Convert TITLE to tag."
    (concat "@" (s-replace " " "" title)))
#+end_src

And that's it! Keep in mind, that with this approach automatic tagging does not
happen when mentioning people using company interface (e.g. using
=org-roam-company=) and when using =vulpea-insert= to mention someone unknown to
your system (e.g. when you create a new file). Both of the 'features' can be
implemented using hooks.

P.S. =vulpea-insert= can be modified to skip tagging when called with
universal argument. Feel free to reach me if you need help with that!

* Task Management with org-roam Series

1. [[https://d12frosted.io/posts/2020-06-23-task-management-with-roam-vol1.html][Path to Roam]]
2. [[https://d12frosted.io/posts/2020-06-24-task-management-with-roam-vol2.html][Categories]]
3. [[https://d12frosted.io/posts/2020-06-25-task-management-with-roam-vol3.html][FILETAGS]]
4. [[https://d12frosted.io/posts/2020-07-07-task-management-with-roam-vol4.html][Automatic tagging]]
5. [[https://d12frosted.io/posts/2021-01-16-task-management-with-roam-vol5.html][Dynamic and fast agenda]]
6. [[https://d12frosted.io/posts/2021-01-24-task-management-with-roam-vol6.html][Select a person and view related tasks]]
7. [[https://d12frosted.io/posts/2021-05-21-task-management-with-roam-vol7.html][Capture]]

* References

- =org-roam= documentation on [[https://github.com/org-roam/org-roam][GitHub]].
- =org-mode= documentation on the [[https://orgmode.org][official site]].
- Org-roam tags on [[https://d12frosted.io/posts/2020-06-10-org-roam-tags.html][d12frosted.io]].
- personal configurations on [[https://github.com/d12frosted/environment/blob/master/emacs/lisp/%2Borg-notes.el][GitHub]].
